/**
 * Compute the atmosphere color, applying Rayleigh and Mie scattering. This
 * builtin uses automatic uniforms so the atmophere settings are synced with the
 * state of the Scene, even in other contexts like Model.
 *
 * @name czm_computeAtmosphereColor
 * @glslFunction
 *
 * @param {vec3} positionWC Position of the fragment in world coords (low precision)
 * @param {vec3} lightDirection Light direction from the sun or other light source.
 * @param {vec3} rayleighColor The Rayleigh scattering color computed by a scattering function
 * @param {vec3} mieColor The Mie scattering color computed by a scattering function
 * @param {float} opacity The opacity computed by a scattering function.
 */
vec4 czm_computeAtmosphereColor(
    vec3 positionWC,
    vec3 lightDirection,
    vec3 rayleighColor,
    vec3 mieColor,
    float opacity
) {
    // Setup the primary ray: from the camera position to the vertex position.
    vec3 cameraToPositionWC = positionWC - czm_viewerPositionWC;
    vec3 cameraToPositionWCDirection = normalize(cameraToPositionWC);

    float cosAngle = dot(cameraToPositionWCDirection, lightDirection);
    float cosAngleSq = cosAngle * cosAngle;

    float G = czm_atmosphereMieAnisotropy;
    float GSq = G * G;

    // The Rayleigh phase function. 
    // 瑞利散射相位函数 https://zhuanlan.zhihu.com/p/210745877
    // 3 / (16π) * (1 + cos²θ)
    float rayleighPhase = 3.0 / (50.2654824574) * (1.0 + cosAngleSq);
    // The Mie phase function.
    // 米氏散射的相函数公式（简化版）: (1 - g²) / (1 + g² - 2gcosθ)^(2/3)
    /**
     * float miePhase = (3.0 / (8.0 * PI)) * 
                ((1.0 - G²) * (cosθ² + 1.0)) / 
                ((1.0 + G² - 2.0 * cosθ * G)^(3/2) * (2.0 + G²));
     * 3.0 / (8.0 * PI) 是归一化系数
     * G 是各向异性因子（即 mieAnisotropy，代码中用 G 表示） 确保相函数在所有方向上的积分结果为 1（物理上，散射光在所有方向的总能量应等于入射能量）
     * 角度依赖项：(cosθ² + 1.0) / (2.0 + G²)对米氏散射方向性的更精细描述，尤其在 G 取值较大（强前向散射）时，能更准确地模拟颗粒大小对散射方向的影响。
     * 这个公式属于半经验精确版，介于完全简化版和纯米氏理论级数解之间：
     * 比简化版更接近物理真实（尤其对大颗粒散射的方向描述更准确）
     * 比纯理论解（需要计算复杂的球贝塞尔函数级数）更高效，适合实时渲染
     * 兼顾物理精度和计算效率的 “精确实现版”
    */
    float miePhase = 3.0 / (25.1327412287) * ((1.0 - GSq) * (cosAngleSq + 1.0)) / (pow(1.0 + GSq - 2.0 * cosAngle * G, 1.5) * (2.0 + GSq));
    
    // The final color is generated by combining the effects of the Rayleigh and Mie scattering.
    vec3 rayleigh = rayleighPhase * rayleighColor;
    vec3 mie = miePhase * mieColor;

    vec3 color = (rayleigh + mie) * czm_atmosphereLightIntensity;

    return vec4(color, opacity);
}

/**
 * Compute the atmosphere color, applying Rayleigh and Mie scattering. This
 * builtin uses automatic uniforms so the atmophere settings are synced with the
 * state of the Scene, even in other contexts like Model.
 *
 * @name czm_computeAtmosphereColor
 * @glslFunction
 *
 * @param {czm_ray} primaryRay Ray from the origin to sky fragment to in world coords (low precision)
 * @param {vec3} lightDirection Light direction from the sun or other light source.
 * @param {vec3} rayleighColor The Rayleigh scattering color computed by a scattering function
 * @param {vec3} mieColor The Mie scattering color computed by a scattering function
 * @param {float} opacity The opacity computed by a scattering function.
 */
vec4 czm_computeAtmosphereColor(
    czm_ray primaryRay,
    vec3 lightDirection,
    vec3 rayleighColor,
    vec3 mieColor,
    float opacity
) {
    /**
     * 瑞利相位：仅与cosAngle²相关，角度变化对强度影响小，所以天空整体呈均匀的淡蓝色；
     * 米氏相位：受G（各向异性）影响大，当cosAngle=1（正对太阳）时，相位值最大，会呈现明显的太阳光晕；
     * 当cosAngle减小时，相位值快速下降，光晕消失 —— 这完全符合真实大气的视觉效果。
    */
    //
    vec3 direction = normalize(primaryRay.direction);
    // 计算视线方向与光源方向的点积（即cosθ，θ为两者夹角）
    float cosAngle = dot(direction, lightDirection);
    float cosAngleSq = cosAngle * cosAngle;
    // // 米氏散射的各向异性参数（控制光晕的集中程度，Cesium中通常设为0.76，模拟真实大气）
    float G = czm_atmosphereMieAnisotropy;
    float GSq = G * G;
    // https://zhuanlan.zhihu.com/p/210745877
    // The Rayleigh phase function. 瑞利散射相位函数（偏蓝光，角度影响较弱，公式源于物理模型）
    // 波长（通常用 λ 表示）在这个简化的相位函数中被省略了，因为该公式仅关注散射角度的分布特性，而非与波长相关的绝对散射强度
    float rayleighPhase = 3.0 / (50.2654824574) * (1.0 + cosAngleSq);
    // （注：50.265...是4π×4，用于归一化，确保散射强度在合理范围）
    // The Mie phase function.  米氏散射相位函数（偏白光/光晕，角度影响强，正对光源时强度最高）
    float miePhase = 3.0 / (25.1327412287) * ((1.0 - GSq) * (cosAngleSq + 1.0)) / (pow(1.0 + GSq - 2.0 * cosAngle * G, 1.5) * (2.0 + GSq));
    // （注：25.132...是2π×4，同样用于归一化）

    /**
     * 将 “散射分量” 与 “相位函数” 相乘（得到角度修正后的散射强度），再叠加光源强度，最终输出带透明度的颜色
    */
    // The final color is generated by combining the effects of the Rayleigh and Mie scattering.
    // 用相位函数修正散射分量（角度不同，最终颜色强度不同）
    // 修正后的瑞利散射颜色（淡蓝）
    vec3 rayleigh = rayleighPhase * rayleighColor;
    // 修正后的米氏散射颜色（光晕）
    vec3 mie = miePhase * mieColor;
    // czm_atmosphereLightIntensity：是 Cesium 的全局参数，可控制大气亮度（比如夜晚时调小，让大气颜色变暗）；
    vec3 color = (rayleigh + mie) * czm_atmosphereLightIntensity;

    return vec4(color, opacity);
}

